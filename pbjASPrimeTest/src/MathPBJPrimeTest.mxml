<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" applicationComplete="init()">

  <mx:Script>
    <![CDATA[
      import pbjAS.params.Texture;
      import mathpbj.MathFunctionJob;
      import mathpbj.MathEvent;

      private var primes:Vector.<Number>;

      private var startTime:Date;

      private var mathFunc:MathFunctionJob;

      private function init():void
      {
        primes = new Vector.<Number>();
        
        mathFunc = new MathFunctionJob();
        mathFunc.addEventListener(MathEvent.COMPLETE, handleMathFuncComplete);
        mathFunc.add("div");
        
        var t:Timer = new Timer(250);
        t.addEventListener(TimerEvent.TIMER, tick);
        t.start();
      }
      
      private function tick(event:TimerEvent):void
      {
        ta.text = primes.toString();
      }

      private function startPBJ():void
      {
        ta.text = "";

        primes = new Vector.<Number>();

        var l:Number = new Number(limit.selectedItem);

        startTime = new Date();

        var texture:Vector.<Number> = new Vector.<Number>();

        for (var i:Number = 2; i <= l; i++)
        {
          texture.push(i);
        }
        
        findPrimes(texture, 2);
      }

      private function findPrimes(texture:Vector.<Number>, evaluator:Number):void
      {
        mathFunc.input = texture;
        mathFunc.inputParams["divisor"] = evaluator;
        mathFunc.start();
      }

      private function handleMathFuncComplete(event:MathEvent):void
      {
        var results:Vector.<Number> = event.results;

        var texture:Vector.<Number> = new Vector.<Number>();
        
        if (results[0] == 1)
        {
          primes.push(event.currentTarget.input.shift());
          results.shift();
        }
        
        // dump the possible primes that had a whole number result
        /*
        // results gets past as "this" only when the function is not a class member, ie. anonymous
        texture = texture.filter(function(item:Number, index:int, vector:Vector.<Number>):Boolean
          {
            return (new uint(this[index]) != this[index]);
            //return (this[index] % 1) != 0;
          }, results);
        */
        var i:uint = 0;
        for each (var n:Number in results)
        {
          if ((n % 1) != 0)
          {
            texture.push(event.currentTarget.input[i]);
          }
          
          i++;
        }
        
        trace(texture.length);

        // if there are more numbers left then run it again
        if (texture.length > 0)
        {
          findPrimes(texture, texture[0]);
        }
        else
        {
          rl.text = (new Date().time - startTime.time) + " ms";
        }
      }

    ]]>
  </mx:Script>

  <mx:Panel title="Primes" width="100%" height="100%">
    <mx:Label id="rl"/>
    <mx:TextArea id="ta" width="100%" height="100%" focusEnabled="false" borderStyle="none" editable="false"/>
    <mx:ControlBar>
      <mx:ComboBox id="limit">
        <mx:dataProvider>
          <mx:Array>
            <mx:Number>10</mx:Number>
            <mx:Number>100</mx:Number>
            <mx:Number>1000</mx:Number>
            <mx:Number>10000</mx:Number>
            <mx:Number>100000</mx:Number>
            <mx:Number>1000000</mx:Number>
          </mx:Array>
        </mx:dataProvider>
      </mx:ComboBox>
      <mx:Button label="Find Primes!" click="startPBJ()"/>
    </mx:ControlBar>
  </mx:Panel>

</mx:Application>